#!/bin/sh
#|-*- mode:lisp -*-|#
#| <Put a one-line description here>
exec ros -L sbcl-bin -m update-asdf -Q -- $0 "$@"
|#
(ql:quickload '(:trivia :asd-generator) :silent t)
(defpackage :ros.script.update-asdf.3672149214
  (:use :cl :asd-generator :trivia))
(in-package :ros.script.update-asdf.3672149214)

(defvar *im-sure* nil)
(defvar *system* nil)
(defvar *data* "asd-generator-data.asd")

(defun infer-system ()
  "Select the first system in the current directory."
  (list
   (first
    (sort
     (remove "asd-generator-data"
             (mapcar #'pathname-name (directory #p"./*.asd"))
             :test #'string=)
     #'string<))))

(defun main (&rest argv)
  (declare (ignorable argv))
  (match argv
    ((list* "-C" pathname rest)
     (let ((*default-pathname-defaults* (pathname pathname)))
       (apply #'main rest)))
    ((list* "-y" rest)
     (let ((*im-sure* t))
       (apply #'main rest)))
    ((list* "-r" rest)
     (ql:register-local-projects)
     (apply #'main rest))
    ((list* "-s" system rest)
     (let ((*system* (cons system *system*)))
       (apply #'main rest)))
    ((list* "-f" data rest)
     (let ((*data* data))
       (apply #'main rest)))
    (nil
     (dolist (s (or *system* (infer-system)))
       (write-asd s :im-sure *im-sure* :data *data*)))
    (_
     (format *error-output*
             "Usage: update-asdf [-y] [-r] [-C pathname] [[-s SYSTEM]*] [-f asd-generator-data.asd]

* -y          : Do not confirm; equivalent to `:im-sure t`.

* -C pathname : Specify the system pathname. Similar to `make -C path`

* -s SYSTEM   : Specify which asdf system definition to update.
                This is because the current directory could contain multiple asdf definitions.
                By default, it selects the first system in the dictionary order of the names.
                Can be specified multiple times, in which case all definitions are updated.
                Do not include `.asd` in the system name.

* -f data     : Specify the asd-generator-data file relative to the system pathname,
                or an absolute pathname. Defaulted to `asd-generator-data.asd`.
                Similar to `Makefile -f makefile.mk` .

* -r          : Call ql:register-local-projects
"))))

;;; vim: set ft=lisp lisp:
